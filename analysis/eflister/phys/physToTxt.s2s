'the following are filled in by matlab automatically

'const base$           := ;
'const expPath$        := ;
'const sTime           := ;
'const eTime           := ;

'one of the following chunks will be filled in, the other should all be set to -1

'const physChan%      := ;       %the high fidelity one
'const stimChan%      := ;       %photodiode recording
'var pulses%[]      := ;

'const spkChan%       := ;        %wavemark chan (all codes will be considered one class, so we hide those we don't want exported)
'const spkCode%       := ;

'''''''''' dont edit below here '''''''''''''

ChanProcessClear(); 'removes all processes from all channels

'channel type codes for ExportChanFormat
'1	Waveform				
'2	Event (Evt-)		
'3	Event (Evt+)
'4	Level (Evt+-)
'5	Marker 
'6	WaveMark
'7	RealMark
'8	TextMark	
'9	RealWave

ExportChanList(1);                     'clear export list, set zero shift
'actually, i think i was misreading the doc
'now i think it says that supplying 1 will tell it to shift the earliest time to zero!
'that is NOT what we want
'but fortunately i think it only affects saving of .smr files, not .txt files
'for now leaving as is, cuz it seems to work
'also affects each call below

ExportTextFormat();                    'reset export params
ExportTextFormat(0, 0, 1, "%", "\t");  '(head%, sum%, cols%{, delim${, sep$}})  "\""

if spkChan%>0 and spkCode%>-1 then
    ExportChanList(sTime, eTime, spkChan%);

    ExportChanFormat(6,0,1,3);			   'type%, synop%, data%{, as%}   -switch to as=5 to do multiple cells-   (supposed to return error code but doesn't)?

    Try(MarkMask(spkChan%,0),"setting mask mode to AND all 4 layers");
    Try(MarkMask(spkChan%,-1,1,-1),"setting mask to all visible");
    Try(MarkMask(spkChan%,0,0,-1),"setting mask to empty 1st layer");
    Try(MarkMask(spkChan%,0,1,spkCode%),"setting mask to just spkCode in 1st layer");

    doSave("spks");

    Try(MarkMask(spkChan%,-1,1,-1),"resetting mask to all visible");

    ExportChanFormat(6,0,1,0);
    doSave("waveforms");
endif;

if stimChan%>0 then
    ExportChanList(1);
    ExportChanList(sTime, eTime, stimChan%);
    doSave("stim");
endif;

if physChan%>0 then
    ExportChanList(1);
    ExportChanList(sTime, eTime, physChan%);
    doSave("phys");
endif;

if Len(pulses%[])>1 then
    ExportChanList(1);
    ExportChanList(sTime, eTime, pulses%[]);
    'ExportChanFormat(4,0,1,2);	'doesn't seem to have desired effect of not showing both edges of pulses, so we compensate in matlab
    ExportChanFormat(6,0,1,3);  'indexPulses are wavemarks, we just want events
    doSave("pulse");
    ExportChanFormat(6,0,1,0);
endif;

FileClose(-1,-1);
FileQuit();
Halt;

proc doSave(prefix$)
	'consider err% := FilePathSet(expPath$,0,1); 'but then reset to previous
	Try(FileSaveAs(expPath$+prefix$+"."+base$+".txt", 1),"writing "+prefix$+" file"); 'will prompt to overwrite
end;

proc Try(succ%, s$)
	if succ%<0 then
		PrintLog("\n***ERROR:  %s: %s (code %d)\n",s$,Error$(succ%),succ%);
		Halt;
	endif;
end;
